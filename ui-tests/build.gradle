ext {
    allure_selenide_version = '2.9.0'
    lombok_version = '1.18.6'
    selenide_version = '5.2.0'
    webdrivermanager_version = '3.2.0'
}
dependencies {
    compile(
            "io.github.bonigarcia:webdrivermanager:$webdrivermanager_version",
            "com.codeborne:selenide:$selenide_version")

    annotationProcessor "org.projectlombok:lombok:$lombok_version"
}

test {

    doFirst {
        jvmArgs "-javaagent:${configurations.agent.singleFile}"
    }

    useJUnitPlatform()

    systemProperties = [
            'junit.jupiter.execution.parallel.enabled': true,
            'junit.jupiter.execution.parallel.mode.default': 'concurrent',
            'junit.jupiter.execution.parallel.mode.classes.default': 'concurrent'
    ]
}

tasks.withType(Test) {

    beforeTest { descriptor ->
        logger.lifecycle("Running: " + descriptor)
    }

    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
        showStandardStreams = true
        exceptionFormat = 'full'
    }

    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}

allure {
    version = '2.5.0'
    aspectjweaver = true
    configuration = 'compile'
    clean = true

    useJUnit5 {
        version = '2.8.1'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '5.5.1'
}